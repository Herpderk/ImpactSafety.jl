{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "6679865f",
   "metadata": {},
   "outputs": [],
   "source": [
    "using MuJoCo\n",
    "using LinearAlgebra\n",
    "using ForwardDiff\n",
    "using MatrixEquations\n",
    "using Revise\n",
    "using ImpactSafety"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "e9ccba72",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Stabilizing infinite horizon LQR policy\n",
    "\"\"\"\n",
    "\n",
    "mutable struct InfiniteHorizonLQR\n",
    "    qref::Vector\n",
    "    vref::Vector\n",
    "    uref::Vector\n",
    "    K::Matrix\n",
    "end\n",
    "\n",
    "function InfiniteHorizonLQR(\n",
    "    qref::Vector,\n",
    "    vref::Vector,\n",
    "    uref::Vector,\n",
    "    Q::Matrix,\n",
    "    R::Matrix,\n",
    "    A::Matrix,\n",
    "    B::Matrix\n",
    ")::InfiniteHorizonLQR\n",
    "    nx = size(Q)[1]\n",
    "    nu = size(R)[1]\n",
    "    P = zeros(nx, nu)\n",
    "    _, _, K, _ = ared(A, B, R, Q, P)\n",
    "    return InfiniteHorizonLQR(qref, vref, uref, K)\n",
    "end\n",
    "\n",
    "function (lqr::InfiniteHorizonLQR)(\n",
    "    model::Model,\n",
    "    data::Data\n",
    ")::Nothing\n",
    "    qerr = zeros(model.nv)\n",
    "    mj_differentiatePos(model, qerr, 1, lqr.qref, data.qpos)\n",
    "    verr = data.qvel - lqr.vref\n",
    "    xerr = [qerr; verr]\n",
    "    data.ctrl .= lqr.uref .- lqr.K*xerr\n",
    "    return nothing\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1f9dadca",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Obstacle avoidance auxiliary functions\n",
    "\"\"\"\n",
    "\n",
    "function opos(o::Vector)::Vector\n",
    "    return o[1:3]\n",
    "end\n",
    "\n",
    "function ovel(o::Vector)::Vector\n",
    "    return o[4:6]\n",
    "end\n",
    "\n",
    "function ptask(xtask::Vector)::Vector\n",
    "    return xtask[1:3]\n",
    "end\n",
    "\n",
    "function vtask(xtask::Vector)::Vector\n",
    "    return xtask[4:6]\n",
    "end\n",
    "\n",
    "function Φ(dmin::Real, n::Real, k::Real, o::Vector, xtask::Vector)::Vector\n",
    "    rel_dist = opos(o) - ptask(xtask)\n",
    "    d = norm(rel_dist)\n",
    "    d_unit = rel_dist / d\n",
    "\n",
    "    rel_vel = ovel(o) - vtask(xtask)\n",
    "    ḋ = rel_vel' * d_unit\n",
    "    return [n*dmin - n*d - k*ḋ]\n",
    "end\n",
    "\n",
    "function Φ̇cbf(\n",
    "    Φargs::Tuple,\n",
    "    λ::Real,\n",
    "    model::Model,\n",
    "    data::Data,\n",
    "    bodyidx::Int\n",
    ")::Vector\n",
    "    # Get task state\n",
    "    ptask = data.xipos[bodyidx, :]\n",
    "    vtask = data.cvel[bodyidx, 4:6]\n",
    "    xtask = [ptask; vtask]\n",
    "    Φval = Φ(Φargs..., xtask)\n",
    "    return -λ*Φval\n",
    "end\n",
    "\n",
    "function δΦδx(\n",
    "    Φargs::Tuple,\n",
    "    model::Model,\n",
    "    data::Data,\n",
    "    bodyidx::Int\n",
    ")::Matrix\n",
    "    # Get task state\n",
    "    ptask = data.xipos[bodyidx, :]\n",
    "    vtask = data.cvel[bodyidx, 4:6]\n",
    "    xtask = [ptask; vtask]\n",
    "\n",
    "    # Safety index jacobian w.r.t. task state\n",
    "    δΦδxtask = ForwardDiff.jacobian(δxtask -> filt.Φ(Φargs..., δxtask), xtask)\n",
    "\n",
    "    # Task state jacobian w.r.t. joint position\n",
    "    Jtask = MuJoCo.mj_zeros(3, model.nv)\n",
    "    mj_jacBodyCom(model, data, Jtask, nothing, bodyidx)\n",
    "\n",
    "    # Time-derivative of task state jacobian w.r.t. joint position\n",
    "    Jdottask = taskspace_Jdot(model, data, bodyidx)\n",
    "\n",
    "    # Task state jacobian w.r.t. joint states\n",
    "    δxtaskδx = [Jtask zeros(size(Jdottask)); Jdottask Jtask]\n",
    "\n",
    "    # Safety index jacobian w.r.t. joint states\n",
    "    return δΦδxtask * δxtaskδx\n",
    "end\n",
    "\n",
    "function decoupled_Φ(\n",
    "    dmin::Real,\n",
    "    n::Real,\n",
    "    k::Real,\n",
    "    o::Vector,\n",
    "    ptask::Vector,\n",
    "    vtask::Vector\n",
    ")::Vector\n",
    "    rel_dist = opos(o) - ptask\n",
    "    d = norm(rel_dist)\n",
    "    d_unit = rel_dist / d\n",
    "\n",
    "    rel_vel = ovel(o) - vtask\n",
    "    ḋ = rel_vel' * d_unit\n",
    "    return [n*dmin - n*d - k*ḋ]\n",
    "end"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "0ce5a358",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Load model and compute reference control\n",
    "\"\"\"\n",
    "\n",
    "# Get model and data\n",
    "model = load_model(joinpath(\n",
    "    example_model_files_directory(),\n",
    "    \"humanoid\",\n",
    "    \"humanoid.xml\"\n",
    "))\n",
    "data = init_data(model)\n",
    "reset!(model, data)\n",
    "\n",
    "# Set sim time step\n",
    "Δt = 1e-3\n",
    "model.opt.timestep = Δt\n",
    "\n",
    "# Propagate derived quantities\n",
    "mj_forward(model, data)\n",
    "\n",
    "# Set joint accelerations to 0 and adjust height\n",
    "data.qacc .= 0\n",
    "data.qpos[3] += -5.647855e-4\n",
    "\n",
    "# Get actuator forces from inverse dynamics\n",
    "mj_inverse(model, data)\n",
    "println(\"Desired forces: \", data.qfrc_inverse)\n",
    "\n",
    "# Get references\n",
    "uref = pinv(data.actuator_moment)' * data.qfrc_inverse\n",
    "uref = reshape(uref, length(uref))\n",
    "qref = copy(data.qpos)\n",
    "qref = reshape(qref, length(qref))\n",
    "vref = zeros(model.nv)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "1b83e060",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Design cost matrices\n",
    "\"\"\"\n",
    "\n",
    "nu = model.nu # number of actuators/controls\n",
    "nv = model.nv # number of degrees of freedom\n",
    "\n",
    "torso = MuJoCo.body(model, \"torso\")\n",
    "left_foot = MuJoCo.body(model, \"foot_left\")\n",
    "right_foot = MuJoCo.body(model, \"foot_right\")\n",
    "\n",
    "# Get Jacobian for torso CoM\n",
    "reset!(model, data)\n",
    "model.opt.timestep = Δt\n",
    "\n",
    "data.qpos .= qref\n",
    "forward!(model, data)\n",
    "jac_com = mj_zeros(3, nv)\n",
    "mj_jacSubtreeCom(model, data, jac_com, torso.id)\n",
    "\n",
    "# Get left and right foot Jacobians for balancing\n",
    "jac_lfoot = mj_zeros(3, nv)\n",
    "mj_jacBodyCom(model, data, jac_lfoot, nothing, left_foot.id)\n",
    "jac_rfoot = mj_zeros(3, nv)\n",
    "mj_jacBodyCom(model, data, jac_rfoot, nothing, right_foot.id)\n",
    "\n",
    "# Design Q-matrix to balance CoM over foot\n",
    "jac_diff = jac_com .- jac_lfoot .- jac_rfoot\n",
    "Qbalance = jac_diff' * jac_diff\n",
    "\n",
    "# Get indices into relevant sets of joints\n",
    "free_dofs = 1:6\n",
    "body_dofs = 7:nv\n",
    "\n",
    "# Get all the joints using a list comprehension.\n",
    "# We add one to the raw ID to get the Julia 1-based index of the joint.\n",
    "abdomen_dofs = [\n",
    "    jnt.id+1 for jnt in MuJoCo.joints(model) if occursin(\"abdomen\", jnt.name)\n",
    "]\n",
    "left_leg_dofs = [\n",
    "    jnt.id+1 for jnt in MuJoCo.joints(model)\n",
    "    if occursin(\"left\", jnt.name) && any(\n",
    "        occursin(part, jnt.name) for part in (\"hip\", \"knee\", \"ankle\")\n",
    "    )\n",
    "]\n",
    "right_leg_dofs = [\n",
    "    jnt.id+1 for jnt in MuJoCo.joints(model)\n",
    "    if occursin(\"right\", jnt.name) && any(\n",
    "        occursin(part, jnt.name) for part in (\"hip\", \"knee\", \"ankle\")\n",
    "    )\n",
    "]\n",
    "\n",
    "balance_dofs = vcat(abdomen_dofs, left_leg_dofs, right_leg_dofs)\n",
    "other_dofs = setdiff(body_dofs, balance_dofs)\n",
    "\n",
    "println(\"Balance dofs: \", balance_dofs)\n",
    "println(\"Other dofs:   \", other_dofs)\n",
    "\n",
    "# Cost coefficients\n",
    "balance_joint_cost = 1e+1          # Joints can move a bit and still balance\n",
    "other_joint_cost   = 1e-1        # Other joints can do whatever\n",
    "\n",
    "# Construct joint Q matrix\n",
    "Qjoint = Matrix{Float64}(I, nv, nv)\n",
    "Qjoint[free_dofs, free_dofs] *= 0\n",
    "Qjoint[balance_dofs, balance_dofs] *= balance_joint_cost\n",
    "Qjoint[other_dofs, other_dofs] *= other_joint_cost\n",
    "\n",
    "balance_cost = 2e+2\n",
    "\n",
    "Qpos = 2e-1*balance_cost*Qbalance + Qjoint\n",
    "Q = [Qpos zeros(nv,nv); zeros(nv, 2nv)]  + (1e-10) * I # Add ϵI for positive definite Q\n",
    "R = Matrix{Float64}(I, nu, nu)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "66473d11",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Get dynamics jacobians and LQR gains\n",
    "\"\"\"\n",
    "\n",
    "# Initialise the model at our set point\n",
    "reset!(model, data)\n",
    "model.opt.timestep = Δt\n",
    "data.ctrl .= uref\n",
    "data.qpos .= qref\n",
    "\n",
    "# Finite-difference parameters\n",
    "ϵ = 1e-6\n",
    "centered = true\n",
    "\n",
    "# Compute the Jacobians\n",
    "A = mj_zeros(2nv, 2nv)\n",
    "B = mj_zeros(2nv, nu)\n",
    "mjd_transitionFD(model, data, ϵ, centered, A, B, nothing, nothing)\n",
    "A = Matrix(A)\n",
    "B = Matrix(B)\n",
    "\n",
    "# Infinite horizon LQR policy\n",
    "lqr = InfiniteHorizonLQR(qref, vref, uref, Q, R, A, B)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "2edfc370",
   "metadata": {},
   "outputs": [],
   "source": [
    "# Safety filter policy\n",
    "nu = Int(model.nu)\n",
    "ϵ = 1e-6\n",
    "filt = mjSafetyFilter(nu, ϵ, Φ, Φ̇cbf, δΦδx)\n",
    "\n",
    "dmin = 0.4\n",
    "n = 1\n",
    "k = 0.5\n",
    "o = [0, 0, 1, 0, 0, 0]\n",
    "Φargs = (dmin, n, k, o)\n",
    "\n",
    "λ = 1e2\n",
    "Φ̇args = (Φargs, λ)\n",
    "\n",
    "bodyidx = 3"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "4abe6d66",
   "metadata": {},
   "outputs": [],
   "source": [
    "\"\"\"\n",
    "Simulate\n",
    "\"\"\"\n",
    "\n",
    "# Sim horizon\n",
    "N = 2000\n",
    "\n",
    "# State history\n",
    "xhist = zeros(model.nq + model.nv + model.na, N)\n",
    "\n",
    "# Initial conditions\n",
    "reset!(model, data)\n",
    "model.opt.timestep = Δt\n",
    "\n",
    "zoffset = 2\n",
    "data.qpos .= qref\n",
    "data.qpos[3] += zoffset\n",
    "\n",
    "vic = 0.1*rand(3)\n",
    "data.qvel[1:3] .= vic\n",
    "@show vic\n",
    "\n",
    "forward!(model, data)\n",
    "for k = 1:N\n",
    "    xhist[:,k] .= get_physics_state(model, data)\n",
    "    # Activate controller when robot touches ground\n",
    "    if data.qpos[3] < qref[3] + 1e-1\n",
    "        lqr(model, data)\n",
    "        filt(Φargs, Φ̇args, model, data, bodyidx)\n",
    "    end\n",
    "    step!(model, data)\n",
    "end\n",
    "@show argmax(data.xipos[:,3])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d1abb012",
   "metadata": {},
   "outputs": [],
   "source": [
    "init_visualiser()\n",
    "visualise!(model, data, trajectories=[xhist])"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "22c4fad0",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.10.9",
   "language": "julia",
   "name": "julia-1.10"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.10.9"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
